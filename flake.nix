{
  description = "Build a cargo project without extra checks";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    crane = {
      url = "github:ipetkov/crane";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    flake-utils.url = "github:numtide/flake-utils";
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        flake-utils.follows = "flake-utils";
      };
    };  
  };

  outputs = { self, nixpkgs, crane, flake-utils, rust-overlay, ... }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs { inherit system; overlays = [ rust-overlay.overlays.default ]; };

        rust = pkgs.rust-bin.stable.latest.default.override {
          extensions = [ "rust-src" "rust-analyzer" "clippy" ];
        };

        commons = with pkgs; rec {
          buildInputs = [
            libxkbcommon
            libGL
            vulkan-loader

            # WINIT_UNIX_BACKEND=wayland
            wayland
          ];

          libPath = "${lib.makeLibraryPath buildInputs}";
        };

        craneLib = (crane.mkLib pkgs).overrideToolchain rust;
        desktop-background = craneLib.buildPackage {
          src = craneLib.cleanCargoSource (craneLib.path ./.);
          strictDeps = true;

          buildInputs = commons.buildInputs 
            ++ pkgs.lib.optionals pkgs.stdenv.isDarwin [
            # Additional darwin specific inputs can be set here
            pkgs.libiconv
          ];

          nativeBuildInputs = [
            pkgs.makeWrapper
          ];

          postInstall = ''
            wrapProgram "$out/bin/desktop-background" --prefix LD_LIBRARY_PATH : "${commons.libPath}"
          '';

          LD_LIBRARY_PATH = commons.libPath;
          # Additional environment variables can be set directly
          # MY_CUSTOM_VAR = "some value";
        };
      in
      {
        checks = {
          my-crate = desktop-background;
        };

        packages.default = desktop-background;

        apps.default = flake-utils.lib.mkApp {
          drv = desktop-background;
        };

        devShells.default = craneLib.devShell {
          # Inherit inputs from checks.
          checks = self.checks.${system};

          # Inherid inputs from crate buildInputs
          inputsFrom = [ desktop-background ];

          LD_LIBRARY_PATH = commons.libPath;

          # Additional dev-shell environment variables can be set directly
          # MY_CUSTOM_DEVELOPMENT_VAR = "something else";

          # Extra inputs can be added here; cargo and rustc are provided by default.
          packages = [
            # pkgs.ripgrep
          ];
        };
      });
}
